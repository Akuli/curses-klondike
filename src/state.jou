import "stdlib/io.jou"
import "./card.jou"
import "./klondike.jou"


@public
class Args:
    color: bool
    pick: int
    discardhide: bool

    @public
    def print(self) -> None:
        printf("Color: %s\n", "yes" if self.color else "no")
        printf("Pick: %d\n", self.pick)
        printf("Discard hide: %s\n", "yes" if self.discardhide else "no")


@public
class AppState:
    args: Args

    # For error messages and such
    program_name: byte*

    # Has initscr() been called successfully? If yes, we need to restore
    # terminal to its original state with endwin() when exiting.
    screen_inited: bool

    # The main game state
    klon: Klondike

    # What card does user have selected? NULL if empty place selected.
    sel_card: Card*

    # Where is the selected card? This is especially important when the user
    # selects an empty place.
    sel_place: Card**   # one of: &stock, &discard, &foundations[i], &tableau[i]

    # If user is currently moving cards, this is where they go. NULL means not moving.
    move_dest: Card**   # one of: NULL, &stock, &discard, &foundations[i], &tableau[i]
