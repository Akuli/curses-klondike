import "stdlib/io.jou"
import "stdlib/mem.jou"
import "stdlib/str.jou"
import "stdlib/process.jou"
import "./util.jou"


# TODO: add these to Jou standard library?
declare chdir(path: byte*) -> int
def WIFEXITED(status: int) -> bool:
    return (status & 0x7F) == 0
def WEXITSTATUS(status: int) -> int:
    # TODO: add bitshift operators to Jou. This should be >> 8
    return (status & 0xFF00) / 256


def compile_parser_to_tests_temp_folder() -> None:
    chdir("tests")

    # Sanity check, we should now be in tests folder
    f = fopen("test_args.jou", "r")
    assert f != NULL
    fclose(f)

    # The main function of args.jou prints the parsed arguments. Let's compile that.
    ret = system("mkdir -p tmp && jou ../src/args.jou -o tmp/args")
    assert ret == 0


def read_file(path: byte*, expected: byte*) -> None:
    maxlen = 10000
    file_content = malloc(maxlen + 1)
    assert file_content != NULL
    memset(file_content, 0, maxlen + 1)

    f = fopen(path, "r")
    assert f != NULL
    fread(file_content, 1, maxlen, f)

    if strcmp(file_content, expected) != 0:
        printf(
            "\n\noutputs differ\n=== expected (%lld bytes) ===\n%s\n=== actual (%lld bytes) ===\n%s\n",
            strlen(expected), expected,
            strlen(file_content), file_content,
        )
        fflush(stdout)
        abort()

    free(file_content)
    fclose(f)


def run_command(command: byte*) -> int:
    actual_command: byte[500]
    snprintf(actual_command, sizeof(actual_command), "%s > tmp/out.txt 2>tmp/err.txt", command)

    ret = system(actual_command)
    assert WIFEXITED(ret)  # exited normally, no crash or similar
    return WEXITSTATUS(ret)


def test_help() -> None:
    output = "\
Usage: tmp/args [--help] [--no-colors] [--pick n] [--discard-hide]\n\n\
Options:\n\
  --help          show this help message and exit\n\
  --no-colors     don't use colors, even if the terminal supports colors\n\
  --pick n        pick n cards from stock at a time, default is 3\n\
  --discard-hide  only show topmost discarded card (not useful with --pick=1)\n\
"
    ret = run_command("tmp/args --help")
    read_file("tmp/out.txt", output)
    read_file("tmp/err.txt", "")
    assert ret == 0


def test_defaults() -> None:
    ret = run_command("tmp/args")
    read_file("tmp/out.txt", "Color: yes\nPick: 3\nDiscard hide: no\n")
    read_file("tmp/err.txt", "")
    assert ret == 0


def test_no_defaults() -> None:
    ret = run_command("tmp/args --no-color --pick=2 --discard-hide")
    read_file("tmp/out.txt", "Color: no\nPick: 2\nDiscard hide: yes\n")
    read_file("tmp/err.txt", "")
    assert ret == 0


def test_errors() -> None:
    ret = run_command("tmp/args --wut")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: unknown option '--wut'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    ret = run_command("tmp/args --wut")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: unknown option '--wut'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    # TODO: test ambiguous option if there will ever be an ambiguous option

    ret = run_command("tmp/args wut")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: unexpected argument: 'wut'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    ret = run_command("tmp/args --no-colors lel")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: use just '--no-colors', not '--no-colors something' or '--no-colors=something'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    ret = run_command("tmp/args --no-colors --no-colors")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: repeated option '--no-colors'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    ret = run_command("tmp/args --no-colors --no-colors --no-colors --no-colors")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: repeated option '--no-colors'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    ret = run_command("tmp/args --no-colors --pick")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: use '--pick something' or '--pick=something', not just '--pick'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    ret = run_command("tmp/args --pick a")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: '--pick' wants an integer between 1 and 24, not 'a'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    ret = run_command("tmp/args --pick 1 2")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: unexpected argument: '2'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    ret = run_command("tmp/args --pick 0")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: '--pick' wants an integer between 1 and 24, not '0'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    ret = run_command("tmp/args --pick=0")
    read_file("tmp/out.txt", "")
    read_file("tmp/err.txt", "tmp/args: '--pick' wants an integer between 1 and 24, not '0'\nRun 'tmp/args --help' for help.\n")
    assert ret == 2

    ret = run_command("tmp/args --pick=1")
    read_file("tmp/out.txt", "Color: yes\nPick: 1\nDiscard hide: no\n")
    read_file("tmp/err.txt", "")
    assert ret == 0


def test_nused_bug() -> None:
    # The bug was that --no-color got ignored
    ret = run_command("tmp/args --pick=1 --no-color")
    read_file("tmp/out.txt", "Color: no\nPick: 1\nDiscard hide: no\n")
    read_file("tmp/err.txt", "")
    assert ret == 0


def main() -> int:
    compile_parser_to_tests_temp_folder()
    dot()
    test_help()
    dot()
    test_defaults()
    dot()
    test_no_defaults()
    dot()
    test_errors()
    dot()
    test_nused_bug()
    dot()

    printf(" ok\n")
    return 0
