import "stdlib/io.jou"
import "stdlib/ascii.jou"
import "stdlib/mem.jou"
import "stdlib/str.jou"
import "stdlib/process.jou"
import "../src/args.jou"
import "./util.jou"

declare tmpfile() -> FILE*


def read_file(f: FILE*, expected: byte*) -> None:
    maxlen = 10000
    file_content = malloc(maxlen + 1)
    assert file_content != NULL
    memset(file_content, 0, maxlen + 1)

    rewind(f)
    fread(file_content, 1, maxlen, f)

    if strcmp(file_content, expected) != 0:
        printf(
            "\n\noutputs differ\n=== expected (%lld bytes) ===\n%s\n=== actual (%lld bytes) ===\n%s\n",
            strlen(expected), expected,
            strlen(file_content), file_content,
        )
        fflush(stdout)
        abort()

    free(file_content)
    fclose(f)


class Results:
    out: FILE*
    err: FILE*
    args: Args
    exit_code: int


def parse_and_capture(argv: byte*) -> Results:
    argv_dup = strdup(argv)
    argv_list = split_by_ascii_whitespace(argv_dup)
    argv_list.append(NULL)

    r = Results{out=tmpfile(), err=tmpfile()}
    r.exit_code = parse_args(argv_list.ptr, &r.args, r.out, r.err)

    free(argv_dup)
    free(argv_list.ptr)
    return r


def test_help() -> None:
    r = parse_and_capture("asdasd --help")
    read_file(r.out, "\
Usage: asdasd [--help] [--no-colors] [--pick n] [--discard-hide]\n\n\
Options:\n\
  --help          show this help message and exit\n\
  --no-colors     don't use colors, even if the terminal supports colors\n\
  --pick n        pick n cards from stock at a time, default is 3\n\
  --discard-hide  only show topmost discarded card (not useful with --pick=1)\n\
")
    read_file(r.err, "")
    assert r.exit_code == 0


def test_defaults() -> None:
    r = parse_and_capture("asdasd")
    read_file(r.out, "")
    read_file(r.err, "")
    assert r.exit_code == CONTINUE_TO_PROGRAM

    assert r.args.color
    assert r.args.pick == 3
    assert not r.args.discardhide


def test_no_defaults() -> None:
    r = parse_and_capture("asdasd --no-color --pick=2 --discard-hide")
    read_file(r.out, "")
    read_file(r.err, "")
    assert r.exit_code == CONTINUE_TO_PROGRAM

    assert not r.args.color
    assert r.args.pick == 2
    assert r.args.discardhide


def test_errors() -> None:
    r = parse_and_capture("asdasd --wut")
    read_file(r.out, "")
    read_file(r.err, "asdasd: unknown option '--wut'\nRun 'asdasd --help' for help.\n")
    assert r.exit_code == 2

    # TODO: test ambiguous option if there will ever be an ambiguous option

    r = parse_and_capture("asdasd wut")
    read_file(r.out, "")
    read_file(r.err, "asdasd: unexpected argument: 'wut'\nRun 'asdasd --help' for help.\n")
    assert r.exit_code == 2

    r = parse_and_capture("asdasd --no-colors lel")
    read_file(r.out, "")
    read_file(r.err, "asdasd: use just '--no-colors', not '--no-colors something' or '--no-colors=something'\nRun 'asdasd --help' for help.\n")
    assert r.exit_code == 2

    r = parse_and_capture("asdasd --no-colors --no-colors")
    read_file(r.out, "")
    read_file(r.err, "asdasd: repeated option '--no-colors'\nRun 'asdasd --help' for help.\n")
    assert r.exit_code == 2

    r = parse_and_capture("asdasd --no-colors --no-colors --no-colors --no-colors")
    read_file(r.out, "")
    read_file(r.err, "asdasd: repeated option '--no-colors'\nRun 'asdasd --help' for help.\n")
    assert r.exit_code == 2

    r = parse_and_capture("asdasd --no-colors --pick")
    read_file(r.out, "")
    read_file(r.err, "asdasd: use '--pick n' or '--pick=n', not just '--pick'\nRun 'asdasd --help' for help.\n")
    assert r.exit_code == 2

    r = parse_and_capture("asdasd --pick a")
    read_file(r.out, "")
    read_file(r.err, "asdasd: '--pick' wants an integer between 1 and 24, not 'a'\nRun 'asdasd --help' for help.\n")
    assert r.exit_code == 2

    r = parse_and_capture("asdasd --pick 1 2")
    read_file(r.out, "")
    read_file(r.err, "asdasd: unexpected argument: '2'\nRun 'asdasd --help' for help.\n")
    assert r.exit_code == 2

    r = parse_and_capture("asdasd --pick 0")
    read_file(r.out, "")
    read_file(r.err, "asdasd: '--pick' wants an integer between 1 and 24, not '0'\nRun 'asdasd --help' for help.\n")
    assert r.exit_code == 2

    r = parse_and_capture("asdasd --pick=0")
    read_file(r.out, "")
    read_file(r.err, "asdasd: '--pick' wants an integer between 1 and 24, not '0'\nRun 'asdasd --help' for help.\n")
    assert r.exit_code == 2

    r = parse_and_capture("asdasd --pick=1")
    read_file(r.out, "")
    read_file(r.err, "")
    assert r.exit_code == CONTINUE_TO_PROGRAM
    assert r.args.pick == 1


def test_nused_bug() -> None:
    r = parse_and_capture("asdasd --pick=1 --no-color")
    read_file(r.out, "")
    read_file(r.err, "")
    assert r.exit_code == CONTINUE_TO_PROGRAM
    assert not r.args.color  # the bug was that --no-color got ignored


def main() -> int:
    test_help()
    dot()
    test_defaults()
    dot()
    test_no_defaults()
    dot()
    test_errors()
    dot()
    test_nused_bug()
    dot()

    printf("\n\nok\n")
    return 0
